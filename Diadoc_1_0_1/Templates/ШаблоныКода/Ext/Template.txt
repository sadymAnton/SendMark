#Область ПодключаемыйМодуль_НачалоПримера

Перем ТиповойМодульДиадока;
Перем ОсновнойМодуль Экспорт;


//**************************************
//{ СЕРВИСНЫЕ ФУНКЦИИ

// Обеспечивает контроль совместимости подключаемого модуля и основного	
Функция ЭДО_ВерсияAPIПодключаемогоМодуля() Экспорт
	Возврат 1;
КонецФункции

Функция НовыйКонтент(ИмяКонтента)
	//для отладки/разработки
	Возврат ОсновнойМодуль.ДД_Компонента_ПолучитьПустойКонтент(ИмяКонтента,Истина);
КонецФункции

// прослойка для отладки: перехватывает исключение при установке значения неподходящего типа в реквизит.
Процедура УстановитьЗначениеXDTO(Элемент,ИмяРеквизита,ЗначениеРеквизита,ЭтоСсылка = Ложь)

	Если ЗначениеРеквизита=Неопределено
		Или ЗначениеРеквизита=NULL Тогда
		Возврат;
	КонецЕсли;
	
	Попытка
		//здесь можно добавить проверки на заполненность, вывод сообщений и т.д.
		
		Если ЭтоСсылка Тогда
			Элемент[ИмяРеквизита] = ЗначениеВСтрокуВнутр(ЗначениеРеквизита);
		Иначе	
			Элемент[ИмяРеквизита] = ЗначениеРеквизита;
		КонецЕсли;
		
		//Элемент.Установить(ИмяРеквизита,ЗначениеРеквизита);
		
	Исключение
		Сообщить("Исключение при заполнении реквизита """+ИмяРеквизита+""":
		|"+ОписаниеОшибки());
	КонецПопытки;

КонецПроцедуры

// Может пригодиться при отладке в режиме управляемых форм, когда точки остановки в открытом модуле не срабатывают.
// Достаточно вставить вызов данной процедуры в нужном месте и включить остановку по ошибке.
Процедура ПриглашениеОтладки()
	
	Попытка
		ВызватьИсключение "catch me if you can";
	Исключение
	КонецПопытки;
	
КонецПроцедуры

Процедура ДобавитьСтрокуВТаблицуЗначений(ТЗ, Значение0=Неопределено, Значение1=Неопределено, Значение2=Неопределено)
	
	СтрокаТЗ = ТЗ.Добавить();
	
	Если Значение1 <> Неопределено Тогда
		СтрокаТЗ[0] = Значение0;
	КонецЕсли;
	
	Если Значение1 <> Неопределено Тогда
		СтрокаТЗ[1] = Значение1;
	КонецЕсли;
	
	Если Значение2 <> Неопределено Тогда
		СтрокаТЗ[2] = Значение2;
	КонецЕсли;
		
КонецПроцедуры

//} СЕРВИСНЫЕ ФУНКЦИИ


//**************************************
//{ ФУНКЦИИ ДЛЯ ПОДКЛЮЧЕНИЯ ТИПОВОГО МОДУЛЯ ДИАДОКА КАК ИСТОЧНИКА ДАННЫХ ДЛЯ ДОКУМЕНТОВ

// Возвращает инициализированный объект типового модуля диадока
Функция ТиповойМодульДиадока()
	
	мРежимОтладки = Ложь;
	
	Если ТиповойМодульДиадока=неопределено Тогда
		
		Попытка
			// вдруг модуль уже был подключен. А мы не можем у менеджера внешних обработок спросить, какие обработки подключены.
			ТиповойМодульДиадока = ВнешниеОбработки.Создать("ТиповойМодульДиадока",Ложь);
		Исключение
			
			Если мРежимОтладки Тогда
				//возьмем файл с диска
				
				ИмяФайлаСМодулем = "c:\temp\Diadoc1C_UF_5_11_07_exports.epf";
				ДД = Новый ДвоичныеДанные(ИмяФайлаСМодулем);
				
			Иначе
				
				//развернем файл из макета
				ДД = ПолучитьМакет("ТиповойМодульДиадок_epf");
				
			КонецЕсли;
			
			АдресВХ = ПоместитьВоВременноеХранилище(ДД);
			ТиповойМодульДиадока = ВнешниеОбработки.Подключить(АдресВХ,"ТиповойМодульДиадока",Ложь);
			УдалитьИзВременногоХранилища(АдресВХ);
			
			ТиповойМодульДиадока = ВнешниеОбработки.Создать("ТиповойМодульДиадока",Ложь);
			
		КонецПопытки;
		
//		ТиповойМодульДиадока.ИнициализироватьМодуль(); // это в ОФ
		ИнициализацияТиповогоМодуляУФ(ТиповойМодульДиадока); // а так будет в УФ
	КонецЕсли;
	
	Возврат ТиповойМодульДиадока;
	
КонецФункции

Процедура ИнициализацияТиповогоМодуляУФ(ТиповойМодульДиадока)
	
	// с этим минимумом он уже взлетает
	ТиповойМодульДиадока.ПараметрыКлиентСервер = Новый Структура;
	
	ТиповойМодульДиадока.ПараметрыКлиентСервер.Вставить("СинонимКонфигурации", Метаданные.Синоним);
	ТиповойМодульДиадока.ПараметрыКлиентСервер.Вставить("ВерсияКонфигурации" , Метаданные.Версия);
	
	ТиповойМодульДиадока.ПараметрыКлиентСервер.Вставить("РежимОтладкиСервера",	Ложь);
	ТиповойМодульДиадока.ПараметрыКлиентСервер.Вставить("ИмяОбработки",			"___");
	ТиповойМодульДиадока.ПараметрыКлиентСервер.Вставить("ПодключаемыйМодуль",	Новый Структура("ИспользоватьМодуль",Ложь));
	
	ТиповойМодульДиадока.ПараметрыКлиентСервер.Вставить("ВременноеХранилище",	Новый Структура);
	
	ТиповойМодульДиадока.ПараметрыКлиентСервер.ВременноеХранилище.Вставить("АдресКэшаСервераНаВремяСеанса",	ПоместитьВоВременноеХранилище(Неопределено));
	ТиповойМодульДиадока.ПараметрыКлиентСервер.ВременноеХранилище.Вставить("АдресНеСуществующиеОбработки",	ПоместитьВоВременноеХранилище(Неопределено));
	ТиповойМодульДиадока.ПараметрыКлиентСервер.ВременноеХранилище.Вставить("АдресПодключенныеОбработки",	ПоместитьВоВременноеХранилище(Неопределено));
	
	ТиповойМодульДиадока.ЗаполнитьМанифест(ТиповойМодульДиадока,"Платформа");
	
	ТиповойМодульДиадока.ПараметрыКлиентСервер.Вставить("МаркерКонфигурации",	ТиповойМодульДиадока.МетодСервера(,"ПолучитьМаркерКонфигурации"));
	ТиповойМодульДиадока.ПараметрыКлиентСервер.Вставить("ИмяФормыИнтеграции",	ТиповойМодульДиадока.МетодСервера(,"ИмяФормыИнтеграции"));
	
КонецПроцедуры

// Трансформация Структуры, выданной типовым модулем Диадока, в XDTO
// Может, стоит затащить это в типовой модуль?
Процедура ЗаполнитьКонтентXDTOПоСтруктуре(Контент,Структура)
	
	Для Каждого Эл Из Структура Цикл
		
		Если ТипЗнч(Эл.Значение)=Тип("Структура") Тогда
			
			Если Эл.Ключ = "ЕдиницаИзмеренияСсылка"
				Или Эл.Ключ = "СсылкаНаЕИ" Тогда
				УстановитьЗначениеXDTO(Контент, Эл.Ключ, Эл.Значение.Ссылка, Истина);
				Продолжить;
			КонецЕсли;
			
			Если НЕ Контент.Свойства().Получить(Эл.Ключ)=Неопределено Тогда
				ЗаполнитьКонтентXDTOПоСтруктуре(Контент[Эл.Ключ],Эл.Значение)
			КонецЕсли;
			
		ИначеЕсли ТипЗнч(Эл.Значение)=Тип("Массив")
			Или ТипЗнч(Эл.Значение)=Тип("ТаблицаЗначений") Тогда
			
			ТипСтроки = Контент.Свойства().Получить(Эл.Ключ).Тип.Имя;
			
			Если ТипЗнч(Эл.Значение) = Тип("ТаблицаЗначений") Тогда
				_Массив = ОсновнойМодуль.ТаблицаЗначений_2_МассивСтруктур(Эл.Значение);
			Иначе
				_Массив = Эл.Значение;
			КонецЕсли;
			
			Для Каждого ЭлементМассива Из _Массив Цикл
				
				НовЭлемент = Контент[Эл.Ключ].Добавить(НовыйКонтент(ТипСтроки));
				ЗаполнитьКонтентXDTOПоСтруктуре(НовЭлемент,ЭлементМассива)
			
			КонецЦикла;
			
		ИначеЕсли ЗначениеЗаполнено(Эл.Значение) Тогда
			
			Если НЕ Контент.Свойства().Получить(Эл.Ключ)=Неопределено Тогда
				
				Если Эл.Ключ = "Price" Тогда
					
					Попытка
						Контент.Price = Эл.Значение;
					Исключение
						Контент.Price = Число(Эл.Значение);
					КонецПопытки;
					
				Иначе
					
					УстановитьЗначениеXDTO(Контент, Эл.Ключ, Эл.Значение, ЭтоИмяСвойстваСсылки(Эл.Ключ));
					
				КонецЕсли;
				
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЦикла;
	
	
КонецПроцедуры

Функция ЭтоИмяСвойстваСсылки(Имя)
	Возврат (
			Имя = "ВалютаСсылка" Или
			Имя = "СтранаПроисхожденияСсылка" Или
			Имя = "ЕдиницаИзмеренияСсылка" Или
			Имя = "Ссылка" Или
			Имя = "СсылкаНаЕИ"
	);
КонецФункции
                                 
//} ФУНКЦИИ ДЛЯ ПОДКЛЮЧЕНИЯ ТИПОВОГО МОДУЛЯ ДИАДОКА КАК ИСТОЧНИКА ДАННЫХ ДЛЯ ДОКУМЕНТОВ


//**************************************
//{		ПОДКЛЮЧЕНИЕ ВО ВНЕШНИЕ ОБРАБОТКИ
	
&Насервере
Функция СведенияОВнешнейОбработке() Экспорт
	
	ПараметрыРегистрации = Новый Структура;
	ПараметрыРегистрации.Вставить("Вид",			 	"ДополнительнаяОбработка");
	ПараметрыРегистрации.Вставить("Наименование", 		"Диадок (СКБ Контур): Подключаемый модуль");
	ПараметрыРегистрации.Вставить("БезопасныйРежим", 	Ложь);
	ПараметрыРегистрации.Вставить("Версия", 			"1.0");
	ПараметрыРегистрации.Вставить("Информация", 		"Модуль работы с электронными первичными документами через Диадок (подключаемая часть)");
	
	ТаблицаКоманд = ПолучитьТаблицуКоманд();
	
	ПараметрыРегистрации.Вставить("Команды", ТаблицаКоманд);
	
	Возврат ПараметрыРегистрации;
	
КонецФункции

&Насервере
Функция ПолучитьТаблицуКоманд()
	
	Команды = Новый ТаблицаЗначений;
	
	Команды.Колонки.Добавить("Представление", 			Новый ОписаниеТипов("Строка")); 
	Команды.Колонки.Добавить("Идентификатор", 			Новый ОписаниеТипов("Строка"));
	Команды.Колонки.Добавить("Использование", 			Новый ОписаниеТипов("Строка"));
	Команды.Колонки.Добавить("ПоказыватьОповещение", 	Новый ОписаниеТипов("Булево"));
	Команды.Колонки.Добавить("Модификатор", 			Новый ОписаниеТипов("Строка"));
	
	Возврат Команды;
	
КонецФункции

&Насервере
Процедура ДобавитьКоманду(ТаблицаКоманд, Представление, Идентификатор, Использование, ПоказыватьОповещение = Ложь, Модификатор = "")
	
	НоваяКоманда=						ТаблицаКоманд.Добавить();
	НоваяКоманда.Представление=			Представление;
	НоваяКоманда.Идентификатор=			Идентификатор;
	НоваяКоманда.Использование=			Использование;
	НоваяКоманда.ПоказыватьОповещение=	ПоказыватьОповещение;
	НоваяКоманда.Модификатор=			Модификатор;
	
КонецПроцедуры

//}		ПОДКЛЮЧЕНИЕ ВО ВНЕШНИЕ ОБРАБОТКИ


//**************************************
//{ СОБЫТИЯ ПОДКЛЮЧАЕМОГО МОДУЛЯ

// Обработка всех событий подключаемого модуля
Функция ОбработатьСобытие(ИмяСобытия,Параметры) Экспорт
	
	Если ИмяСобытия = "ПослеПодготовкиПакета" Тогда
		
		//ПослеПодготовкиПакета(Параметры.Пакет);
		Возврат Неопределено;
		
	ИначеЕсли ИмяСобытия = "ПодготовитьЭлектронныйДокумент" Тогда
		
		ПодготовитьЭлектронныйДокумент(Параметры.Результат,Параметры.ВидДокументаРазвернутый,Параметры.ДополнительныеПараметры);
		Возврат Неопределено;
		
	ИначеЕсли ИмяСобытия = "ПолучитьТекстЗапросаДляСпискаПакетовНаОтправку" Тогда
		
		Возврат ПолучитьТекстЗапросаДляВидаПакета(Параметры.ВидПакетаРазвернутый); // для построения списка пакетов на отправку
		
	ИначеЕсли ИмяСобытия = "ПодготовитьПакет" Тогда
		
		ПодготовитьПакет(Параметры.ВидПакетаРазвернутый, Параметры.СтрокаСписка, Параметры.Пакет); // для подготовки единичного пакета
		Возврат Неопределено;
		
	ИначеЕсли ИмяСобытия = "ПолучитьТаблицуИспользуемыхПакетов" Тогда
		
		Возврат ПолучитьТаблицуИспользуемыхПакетов();
		
	ИначеЕсли ИмяСобытия = "ПолучитьТаблицуИспользуемыхВидовДокументов" Тогда
		
		Возврат ПолучитьТаблицуИспользуемыхВидовДокументов();
		
	КонецЕсли;
	
КонецФункции

// Тут можно переопределить отправителя/получателя/подразделения/заблокированность/тестовость
// Но можно закинуть и в код подготовки пакета.
Процедура ПослеПодготовкиПакета(Пакет)
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ 1
	|	ВЫБОР
	|		КОГДА Диадок_ЮрФизЛица.ID_ВладелецПодразделения <> """"
	|			ТОГДА Диадок_ЮрФизЛица.ID_ВладелецПодразделения
	|		ИНАЧЕ Диадок_ЮрФизЛица.ID
	|	КОНЕЦ КАК ID
	|ИЗ
	|	Справочник.Диадок_ЮрФизЛица КАК Диадок_ЮрФизЛица
	|ГДЕ
	|	Диадок_ЮрФизЛица.СвязанныйСправочник1 = &СвязанныйСправочник1"   //это можно и в коробку попробовать
	);
	Запрос.УстановитьПараметр("СвязанныйСправочник1",Пакет.Данные1С.Организация);
	Пакет.ДанныеДД.OrganizationId = ОсновнойМодуль.ЭДО_Служебные_ПолучитьРезультатЗапроса(Запрос, "ID");
	
	Запрос.УстановитьПараметр("СвязанныйСправочник1",Пакет.Данные1С.Контрагент);
	Пакет.ДанныеДД.CounteragentId = ОсновнойМодуль.ЭДО_Служебные_ПолучитьРезультатЗапроса(Запрос, "ID");
	
	//1. FromDepartmentId - по Организации
	Запрос = Новый Запрос(
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ 1
	|	Диадок_ЮрФизЛица.ID КАК ID
	|ИЗ
	|	Справочник.Диадок_ЮрФизЛица КАК Диадок_ЮрФизЛица
	|ГДЕ
	|	Диадок_ЮрФизЛица.СвязанныйСправочник1 = &СвязанныйСправочник1
	|	И Диадок_ЮрФизЛица.ИмяСправочника = ""ПодразделенияОрганизаций"""
	);
	Запрос.УстановитьПараметр("СвязанныйСправочник1",Пакет.Данные1С.Организация);
	Пакет.ДанныеДД.FromDepartmentId = ОсновнойМодуль.ЭДО_Служебные_ПолучитьРезультатЗапроса(Запрос, "ID");
	
	//2. ToDepartmentId - по Договору
	Запрос = Новый Запрос(
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ 1
	|	Диадок_ЮрФизЛица.ID КАК ID
	|ИЗ
	|	Справочник.Диадок_ЮрФизЛица КАК Диадок_ЮрФизЛица
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ДоговорыКонтрагентов КАК спрДоговорыКонтрагентов
	|		ПО (спрДоговорыКонтрагентов.Ссылка = &ТекущийДоговор)
	|			И Диадок_ЮрФизЛица.СвязанныйСправочник2 = спрДоговорыКонтрагентов.Родитель
	|			И (Диадок_ЮрФизЛица.СвязанныйСправочник2 ССЫЛКА Справочник.ДоговорыКонтрагентов)
	|			И (Диадок_ЮрФизЛица.ИмяСправочника = ""ПодразделенияКонтрагентов"")"
	);
	Запрос.УстановитьПараметр("ТекущийДоговор",Пакет.Данные1С.Документ.ДоговорКонтрагента);
	Пакет.ДанныеДД.ToDepartmentId = ОсновнойМодуль.ЭДО_Служебные_ПолучитьРезультатЗапроса(Запрос, "ID");
	
//	Пакет.ДанныеДД.DelaySend = Истина; //отладка
	
КонецПроцедуры

// Основная точка входа для подготовки исходящего документа
Функция ПодготовитьЭлектронныйДокумент(Результат, ВидДокументаРазвернутый, ДополнительныеПараметры)

	Документ1С					= Результат.Документ1С;
	ВидДокументаНаименование	= ВидДокументаРазвернутый.Наименование;
	ТипКонтента					= ВидДокументаРазвернутый.ТипКонтента;
	
	Если ВРЕГ(ТипКонтента)=ВРЕГ("Torg12SellerContent") Тогда
		
		Допсведения = Неопределено;
		ФИОПодписанта = " ";
		
		XmlTorg12Content= ТиповойМодульДиадока().МетодСервера("Модуль_ИнтеграцияУниверсальный","ПолучитьXmlTorg12Content", Документ1С, Неопределено, Неопределено, ДопСведения, ФИОПодписанта);
		
		ОтвЛица = ОтветственныеЛицаБП.ОтветственныеЛица(Документ1С.Организация,Документ1С.Дата);
		ЗаполнитьПодпись(XmlTorg12Content.SupplyAllowedBy,ОтвЛица.РуководительФИО,ОтвЛица.РуководительДолжностьПредставление);
		
		//СписокОшибок=	  ТиповойМодульДиадока().МетодСервера(,"ВалидацияXmlTorg12Content", XmlTorg12Content);
		//Если ЗначениеЗаполнено(СписокОшибок) Тогда
		//	ВызватьИсключение(СписокОшибок);
		//КонецЕсли; 
		
		ЗаполнитьКонтентXDTOПоСтруктуре(Результат.Content,XmlTorg12Content);
		
	ИначеЕсли ВРЕГ(ТипКонтента)=ВРЕГ("InvoiceContent") Тогда
		
		Если НЕ ТипЗнч(Документ1С) = Тип("ДокументСсылка.СчетФактураВыданный") Тогда
			ВызватьИсключение "Неизвестный тип счета-фактуры";
		КонецЕсли;
		
		Допсведения = Неопределено;
		ФИОПодписанта = " ";
		InvoiceContent=	ТиповойМодульДиадока().МетодСервера("Модуль_ИнтеграцияУниверсальный","ПолучитьInvoiceContent", Документ1С, Неопределено, Допсведения, ФИОПодписанта);
		СписокОшибок= 	ТиповойМодульДиадока().МетодСервера(,"ВалидацияInvoiceContent", InvoiceContent, Документ1С);
		Если ЗначениеЗаполнено(СписокОшибок) тогда
			ВызватьИсключение(СписокОшибок)
		КонецЕсли;	 
		
		ЗаполнитьКонтентXDTOПоСтруктуре(Результат.Content,InvoiceContent);
		
		Возврат Истина;
			
	ИначеЕсли ВРЕГ(ТипКонтента)=ВРЕГ("NonformilizedContent") Тогда
		
		Если ВидДокументаНаименование = "Рассылка_PDF" Тогда
			
			ПолныйПутьКФайлу = Документ1С.ТекущаяВерсияТом.ПолныйПутьWindows + Документ1С.ТекущаяВерсия.ПутьКФайлу;
			Результат.ДвоичныеДанные			= Новый ДвоичныеДанные(ПолныйПутьКФайлу);
			Результат.ИмяФайла					= Документ1С.Наименование;
			
			Возврат Истина;
			
		ИначеЕсли ВидДокументаНаименование = "Транзакционный отчет" Тогда
			
			CcылканаВПФ = Справочники.ДополнительныеОтчетыИОбработки.НайтиПоНаименованию(ВидДокументаНаименование); // предполагаем, что он именно так и будет называться
			// Но вообще это лучше уже прямо ссылкой задавать.
			// Ссылку выпустим в следующих релизах.
			
			ИмяВремФайла = "";
			ПредставлениеФайла = "";
			СформироватьВнешнююПечатнуюФорму(Документ1С,CcылканаВПФ,Неопределено,ИмяВремФайла,ПредставлениеФайла);
			
			Результат.ДвоичныеДанные			= Новый ДвоичныеДанные(ИмяВремФайла);
			Результат.ИмяФайла					= ПредставлениеФайла;
			
			УдалитьФайлы(ИмяВремФайла);
			
			Возврат Истина;
			
		ИначеЕсли ВидДокументаНаименование = "АктСверки" Тогда
			
			ФИОПодписанта = " ";
			СтруктураАктаСверки = ТиповойМодульДиадока().МетодСервера("Модуль_ИнтеграцияУниверсальный", "СформироватьПечатнуюФормуАктаСверки", Документ1С, Неопределено, ФИОПодписанта);
			
			ПолныйПутьКФайлу = СтруктураАктаСверки.ИмяВременногоФайла;
			
			ДвоичныеДанные = Новый ДвоичныеДанные(ПолныйПутьКФайлу);
			Результат.Content.Date						= Документ1С.Дата;
			Результат.Content.Number					= Документ1С.Номер;
			Результат.Content.NeedRecipientSignature	= Ложь;
			Результат.ДвоичныеДанные					= ДвоичныеДанные;
			Результат.ИмяФайла							= СтруктураАктаСверки.ИмяФайла;
			
			Возврат Истина;
			
		Иначе
			
			ВызватьИсключение "Неизвестный тип документа: "+ВидДокументаНаименование;
			
		КонецЕсли;
		
	Иначе
		
		ВызватьИсключение "Неизвестный тип документа: "+ВидДокументаНаименование;
		
	КонецЕсли;
	
КонецФункции

Процедура ЗаполнитьПодпись(КонтентПодписи, ФИО, Должность)
	
	КонтентПодписи.FirstName	= ФИО.Имя;
	КонтентПодписи.SurName		= ФИО.Фамилия;
	КонтентПодписи.Patronymic	= ФИО.Отчество;
	
	КонтентПодписи.JobTitle		= Должность;
	
КонецПроцедуры

// Шаблонная функция для вывода внешней печатной формы.
// Если все ок, возвращает Истина
// Результат сохраняется в параметрах ИмяВремФайла и ПредставлениеФайла. Первые 3 параметра - входные.
Функция СформироватьВнешнююПечатнуюФорму(Документ,	ВнешняяПечатнаяФорма, ДопПараметры=Неопределено, ИмяВремФайла, ПредставлениеФайла)
	
	РезультатПечати = Неопределено;
			
	ОбъектыНазначения = Новый Массив;
	ОбъектыНазначения.Добавить(Документ);
	ПараметрыВывода = Неопределено;
	ПараметрыИсточника = Новый Структура("ИдентификаторКоманды,ОбъектыНазначения","ТранзакционныйОтчет",ОбъектыНазначения);
	ОбъектыПечати = Новый СписокЗначений;
	
	МодульДополнительныеОтчетыИОбработки = ОбщегоНазначения.ОбщийМодуль("ДополнительныеОтчетыИОбработки");
	
	МодульДополнительныеОтчетыИОбработки.ПечатьПоВнешнемуИсточнику(ВнешняяПечатнаяФорма,
		ПараметрыИсточника, РезультатПечати, ОбъектыПечати, ПараметрыВывода);
		
	Если НЕ ТипЗнч(РезультатПечати)=Тип("ТаблицаЗначений")
		Или РезультатПечати.Количество()<>1 Тогда
		ВызватьИсключение "Не удалось сформировать печатную форму """ + ВнешняяПечатнаяФорма + """ объекта " + Документ;
	КонецЕсли;
	
	ИмяВремФайла = ПолучитьИмяВременногоФайла();
	
	РезультатПечати[0].ТабличныйДокумент.Записать(ИмяВремФайла,ТипФайлаТабличногоДокумента.PDF);
	
	ПредставлениеФайла = РезультатПечати[0].СинонимМакета + " № " + Документ.Номер + " от " + Формат(Документ.Дата,"ДФ=dd.MM.yyyy") + ".pdf";
			
	Возврат Истина; // все ок
	
КонецФункции

Функция ПолучитьТекстЗапросаДляВидаПакета(ВидПакетаРазвернутый)
	
	Если НЕ ВидПакетаРазвернутый.Свойство("ID") Тогда
		Сообщить("Отсутствует ID для пакета: " + ВидПакетаРазвернутый.Наименование);
		Возврат Неопределено;
	КонецЕсли;
	
	Если ВидПакетаРазвернутый.ID = "ID_ОсновнойПакет" Тогда
		
		Возврат 
		"ВЫБРАТЬ
		|	Диадок_НастройкиКонтрагентов.Организация.СвязанныйСправочник1 КАК Организация1С,
		|	Диадок_НастройкиКонтрагентов.Контрагент.СвязанныйСправочник1 КАК Контрагент1С,
		|	Диадок_НастройкиКонтрагентов.Организация КАК Организация,
		|	Диадок_НастройкиКонтрагентов.Контрагент КАК Контрагент,
		|	Диадок_НастройкиКонтрагентов.Организация.ID,
		|	Диадок_НастройкиКонтрагентов.Контрагент.ID,
		|	Диадок_НастройкиКонтрагентов.Организация.ID_ОсновноеПодразделение КАК ПодразделениеОрганизацииID,
		|	Диадок_НастройкиКонтрагентов.Контрагент.ID_ОсновноеПодразделение КАК ПодразделениеКонтрагентаID
		|ПОМЕСТИТЬ КэшКонтрагентов
		|ИЗ
		|	РегистрСведений.Диадок_НастройкиКонтрагентов КАК Диадок_НастройкиКонтрагентов
		|ГДЕ
		|	Диадок_НастройкиКонтрагентов.ИмяСвойства = ""Статус""
		|	И Диадок_НастройкиКонтрагентов.Значение = ""IsMyCounteragent""
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Организация,
		|	Контрагент
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	РеализацияТоваровУслуг.Ссылка КАК Документ,
		|	РеализацияТоваровУслуг.Номер КАК НомерДокумента,
		|	РеализацияТоваровУслуг.Дата КАК ДатаДокумента,
		|	РеализацияТоваровУслуг.Контрагент КАК Контрагент,
		|	РеализацияТоваровУслуг.Организация КАК Организация,
		|	РеализацияТоваровУслуг.СуммаДокумента КАК СуммаДокумента,
		|	Диадок_ПакетыДокументов.Ссылка КАК Пакет
		|ИЗ
		|	Документ.РеализацияТоваровУслуг КАК РеализацияТоваровУслуг
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ КэшКонтрагентов КАК КэшКонтрагентов
		|		ПО (КэшКонтрагентов.Контрагент1С = РеализацияТоваровУслуг.Контрагент)
		|			И (КэшКонтрагентов.Организация1С = РеализацияТоваровУслуг.Организация)
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Диадок_ПакетыДокументов КАК Диадок_ПакетыДокументов
		|		ПО РеализацияТоваровУслуг.Ссылка = Диадок_ПакетыДокументов.Документ
		|ГДЕ
		|	РеализацияТоваровУслуг.Дата МЕЖДУ &НачалоПериода И &КонецПериода
		|	И РеализацияТоваровУслуг.Проведен
		|	И Диадок_ПакетыДокументов.Ссылка ЕСТЬ NULL";
		
	ИначеЕсли ВидПакетаРазвернутый.ID = "ID_АктСверки" Тогда
		
		Возврат 
		"ВЫБРАТЬ
		|	Диадок_НастройкиКонтрагентов.Организация.СвязанныйСправочник1 КАК Организация1С,
		|	Диадок_НастройкиКонтрагентов.Контрагент.СвязанныйСправочник1 КАК Контрагент1С,
		|	Диадок_НастройкиКонтрагентов.Организация КАК Организация,
		|	Диадок_НастройкиКонтрагентов.Контрагент КАК Контрагент,
		|	Диадок_НастройкиКонтрагентов.Организация.ID,
		|	Диадок_НастройкиКонтрагентов.Контрагент.ID,
		|	Диадок_НастройкиКонтрагентов.Организация.ID_ОсновноеПодразделение КАК ПодразделениеОрганизацииID,
		|	Диадок_НастройкиКонтрагентов.Контрагент.ID_ОсновноеПодразделение КАК ПодразделениеКонтрагентаID
		|ПОМЕСТИТЬ КэшКонтрагентов4
		|ИЗ
		|	РегистрСведений.Диадок_НастройкиКонтрагентов КАК Диадок_НастройкиКонтрагентов
		|ГДЕ
		|	Диадок_НастройкиКонтрагентов.ИмяСвойства = ""Статус""
		|	И Диадок_НастройкиКонтрагентов.Значение = ""IsMyCounteragent""
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Организация,
		|	Контрагент
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	АктСверкиВзаиморасчетов.Ссылка КАК Документ,
		|	АктСверкиВзаиморасчетов.Номер КАК НомерДокумента,
		|	АктСверкиВзаиморасчетов.Дата КАК ДатаДокумента,
		|	АктСверкиВзаиморасчетов.Контрагент КАК Контрагент,
		|	АктСверкиВзаиморасчетов.Организация КАК Организация,
		|	АктСверкиВзаиморасчетов.Расхождение КАК СуммаДокумента,
		|	Диадок_ПакетыДокументов.Ссылка КАК Пакет
		|ИЗ
		|	Документ.АктСверкиВзаиморасчетов КАК АктСверкиВзаиморасчетов
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ КэшКонтрагентов4 КАК КэшКонтрагентов
		|		ПО (КэшКонтрагентов.Контрагент1С = АктСверкиВзаиморасчетов.Контрагент)
		|			И (КэшКонтрагентов.Организация1С = АктСверкиВзаиморасчетов.Организация)
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Диадок_ПакетыДокументов КАК Диадок_ПакетыДокументов
		|		ПО АктСверкиВзаиморасчетов.Ссылка = Диадок_ПакетыДокументов.Документ
		|ГДЕ
		|	АктСверкиВзаиморасчетов.Дата МЕЖДУ &НачалоПериода И &КонецПериода
		|	И НЕ АктСверкиВзаиморасчетов.ПометкаУдаления
		|	И Диадок_ПакетыДокументов.Ссылка ЕСТЬ NULL";
		
	ИначеЕсли ВидПакетаРазвернутый.ID = "ID_Рассылка" Тогда
		
		Возврат 
		"ВЫБРАТЬ
		|	Диадок_НастройкиКонтрагентов.Организация.СвязанныйСправочник1 КАК Организация1С,
		|	Диадок_НастройкиКонтрагентов.Контрагент.СвязанныйСправочник1 КАК Контрагент1С,
		|	Диадок_НастройкиКонтрагентов.Организация КАК Организация,
		|	Диадок_НастройкиКонтрагентов.Контрагент КАК Контрагент,
		|	Диадок_НастройкиКонтрагентов.Организация.ID,
		|	Диадок_НастройкиКонтрагентов.Контрагент.ID,
		|	Диадок_НастройкиКонтрагентов.Организация.ID_ОсновноеПодразделение КАК ПодразделениеОрганизацииID,
		|	Диадок_НастройкиКонтрагентов.Контрагент.ID_ОсновноеПодразделение КАК ПодразделениеКонтрагентаID
		|ПОМЕСТИТЬ КэшКонтрагентов2
		|ИЗ
		|	РегистрСведений.Диадок_НастройкиКонтрагентов КАК Диадок_НастройкиКонтрагентов
		|ГДЕ
		|	Диадок_НастройкиКонтрагентов.ИмяСвойства = ""Статус""
		|	И Диадок_НастройкиКонтрагентов.Значение = ""IsMyCounteragent""
		|	И НЕ Диадок_НастройкиКонтрагентов.Организация.СвязанныйСправочник1 = НЕОПРЕДЕЛЕНО
		|	И НЕ Диадок_НастройкиКонтрагентов.Контрагент.СвязанныйСправочник1 = НЕОПРЕДЕЛЕНО
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Организация,
		|	Контрагент
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Файлы.Ссылка КАК Документ,
		|	Файлы.Наименование КАК НомерДокумента,
		|	Файлы.ДатаСоздания КАК ДатаДокумента,
		|	КэшКонтрагентов.Контрагент1С КАК Контрагент,
		|	КэшКонтрагентов.Организация1С КАК Организация,
		|	0 КАК СуммаДокумента
		|ПОМЕСТИТЬ ВТДекартоваНаОтправку
		|ИЗ
		|	Справочник.Файлы КАК Файлы,
		|	КэшКонтрагентов2 КАК КэшКонтрагентов
		|ГДЕ
		|	Файлы.ДатаСоздания МЕЖДУ &НачалоПериода И &КонецПериода
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТДекартоваНаОтправку.Документ КАК Документ,
		|	ВТДекартоваНаОтправку.НомерДокумента КАК НомерДокумента,
		|	ВТДекартоваНаОтправку.ДатаДокумента КАК ДатаДокумента,
		|	ВТДекартоваНаОтправку.Контрагент КАК Контрагент,
		|	ВТДекартоваНаОтправку.Организация КАК Организация,
		|	ВТДекартоваНаОтправку.СуммаДокумента КАК СуммаДокумента,
		|	Диадок_ПакетыДокументов.Ссылка КАК Пакет
		|ИЗ
		|	ВТДекартоваНаОтправку КАК ВТДекартоваНаОтправку
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Диадок_ПакетыДокументов КАК Диадок_ПакетыДокументов
		|		ПО ВТДекартоваНаОтправку.Документ = Диадок_ПакетыДокументов.Документ
		|			И ВТДекартоваНаОтправку.Контрагент = Диадок_ПакетыДокументов.Контрагент
		|			И ВТДекартоваНаОтправку.Организация = Диадок_ПакетыДокументов.Организация
		|ГДЕ
		|	Диадок_ПакетыДокументов.Ссылка ЕСТЬ NULL";
		
	Иначе
		
		//ВызватьИсключение "Неизвестный вид пакета: " + ВидПакетаРазвернутый.ID;	
		Сообщить("Неизвестный ID пакета: " + ВидПакетаРазвернутый.ID);
		
	КонецЕсли;
	
КонецФункции

Функция ПодготовитьПакет(ВидПакетаРазвернутый, СтрокаСписка, Пакет)
	
	Если ВидПакетаРазвернутый.Наименование = "Основной пакет" Тогда
		
		СчетФактура = УчетНДСПереопределяемый.НайтиПодчиненныйСчетФактуруВыданныйНаРеализацию(СтрокаСписка.Документ);
		Если Не ЗначениеЗаполнено(СчетФактура) Тогда
			ВызватьИсключение "Отсутствует счет-фактура по документу " + СтрокаСписка.Документ;
		КонецЕсли;

		ОсновнойМодуль.ЭДО_ДокументМенеджер_ПодготовитьИДобавитьДокументВПакет(Пакет,	СчетФактура,			"СчетФактура");
		ОсновнойМодуль.ЭДО_ДокументМенеджер_ПодготовитьИДобавитьДокументВПакет(Пакет,	СтрокаСписка.Документ,	"ТОРГ12");
		//ОсновнойМодуль.ЭДО_ДокументМенеджер_ПодготовитьИДобавитьДокументВПакет(Пакет,	СтрокаСписка.Документ,	"Транзакционный отчет");
		
	ИначеЕсли ВидПакетаРазвернутый.Наименование = "АктСверки" Тогда
		
		ОсновнойМодуль.ЭДО_ДокументМенеджер_ПодготовитьИДобавитьДокументВПакет(Пакет,	СтрокаСписка.Документ,	"АктСверки");

	ИначеЕсли ВидПакетаРазвернутый.Наименование = "Рассылка" Тогда
		
		
		ОсновнойМодуль.ЭДО_ДокументМенеджер_ПодготовитьИДобавитьДокументВПакет(Пакет,	СтрокаСписка.Документ,	"Рассылка_PDF");
		
	Иначе
		
		ВызватьИсключение "Неизвестный вид пакета: " + ВидПакетаРазвернутый.Наименование;	
		
	КонецЕсли;
	
КонецФункции

//Определение используемых пакетов
Функция ПолучитьТаблицуИспользуемыхПакетов()
	
	ТЗ  = Новый ТаблицаЗначений;
	ТЗ.Колонки.Добавить("ID");
	ТЗ.Колонки.Добавить("Наименование");
	
	ДобавитьСтрокуВТаблицуЗначений(ТЗ, "ID_ОсновнойПакет",	"Основной пакет");
	ДобавитьСтрокуВТаблицуЗначений(ТЗ, "ID_АктСверки", 		"Акт сверки");
	ДобавитьСтрокуВТаблицуЗначений(ТЗ, "ID_Рассылка", 		"Рассылка");
	
	Возврат ТЗ;
	
КонецФункции

//Определение используемых видов документов, включаемых в пакеты
Функция ПолучитьТаблицуИспользуемыхВидовДокументов()
	
	ТЗ  = Новый ТаблицаЗначений;
	ТЗ.Колонки.Добавить("ID");
	ТЗ.Колонки.Добавить("Наименование");
	ТЗ.Колонки.Добавить("ТипДокумента");
	
	ДобавитьСтрокуВТаблицуЗначений(ТЗ, "ID_СчетФактура","Счет-фактура", "Invoice");
	ДобавитьСтрокуВТаблицуЗначений(ТЗ, "ID_ТОРГ12", 	"ТОРГ-12", 		"XmlTorg12");
	ДобавитьСтрокуВТаблицуЗначений(ТЗ, "ID_АктСверки", 	"Акт сверки", 	"ReconciliationAct");
	ДобавитьСтрокуВТаблицуЗначений(ТЗ, "ID_Рассылка", 	"Рассылка", 	"Nonformalized");

	Возврат ТЗ;
	
КонецФункции

//} СОБЫТИЯ ПОДКЛЮЧАЕМОГО МОДУЛЯ


//**************************************
//{	МЕТОДЫ ДЛЯ ЗАПОЛНЕНИЯ КОНТЕНТА

#КонецОбласти


#Область ПодключаемыйМодуль_КонецПримера

//}	МЕТОДЫ ДЛЯ ЗАПОЛНЕНИЯ КОНТЕНТА

#КонецОбласти

