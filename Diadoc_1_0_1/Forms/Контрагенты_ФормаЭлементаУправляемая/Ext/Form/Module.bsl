&НаСервере
Перем ОбработкаОбъект;

&НаКлиенте
Перем ОсновнаяФорма;

&НаКлиенте
Функция ОсновнаяФорма() Экспорт

	Если ОсновнаяФорма=Неопределено Тогда
		ОсновнаяФорма = ВладелецФормы.ОсновнаяФорма();
	КонецЕсли;	
	
	Возврат ОсновнаяФорма;
	
КонецФункции

&НаСервере
Функция ОбработкаОбъект()
	Если ОбработкаОбъект=Неопределено Тогда
		ОбработкаОбъект = РеквизитФормыВЗначение("Объект");
	КонецЕсли;
	Возврат ОбработкаОбъект;
КонецФункции

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	Если ЗначениеЗаполнено(Параметры.Ссылка) Тогда
		
		ЗаполнитьРеквизитыФормыНаСервере();
	
		Если Не ЗначениеЗаполнено(СвязанныйСправочник1) Тогда
			//типизируем поле
			ОбработкаОбъект().ЭДО_Модуль_УстановитьТипПоля1С(ЭтаФорма,"СвязанныйСправочник1","Контрагенты");
		КонецЕсли;
		
	КонецЕсли;

	Если Параметры.ИмяСправочника = "ПодразделенияКонтрагентов" Тогда
		
		ЭтаФорма.Заголовок = "Подразделение контрагента";
		Элементы.ГруппаПодразделение.Видимость				= Истина;
		Элементы.ГруппаНастройкиПоОрганизациям.Видимость	= Ложь;

	ИначеЕсли Параметры.ИмяСправочника = "Контрагенты" Тогда
		
		ЭтаФорма.Заголовок = "Контрагент";
		Элементы.ГруппаПодразделение.Видимость				= Ложь;
				
		Если ОбработкаОбъект().ЭДО_Использовать1СЭДО() Тогда			
			
			Элементы.ГруппаНастройкиПоОрганизациям.Видимость = Ложь;
			
			Элементы.ДеревоПодразделенийСвязь1.Видимость = Ложь;
			Элементы.ДеревоПодразделенийСвязь2.Видимость = Ложь;
			Элементы.ДеревоПодразделенийСвязь3.Видимость = Ложь;
			
		КонецЕсли;
		
		Если Параметры.ИмяСправочника <> "Контрагенты"
			Или ОбработкаОбъект().ЭДО_Использовать1СЭДО() Тогда
				Элементы.ФормаОбновитьССервера.Видимость = Ложь;
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьРеквизитыФормыНаСервере()
	
	СтруктураРеквизитов = ОбработкаОбъект().ЭДО_СправочникМенеджер_ПолучитьЭлемент(Параметры.ИмяСправочника, Параметры.Ссылка);
	
	СтрокаРеквизитов = "Наименование,ID,ИНН,КПП,СвязанныйСправочник1,СвязанныйСправочник2,СвязанныйСправочник3,Аббревиатура,ID_ВладелецПодразделения,ID_РодительПодразделения,ID_ОсновноеПодразделение";
	
	Для Каждого ИмяРеквизита Из ОбработкаОбъект().ЭДО_Служебные_РазложитьСтрокуВМассивСлов(СтрокаРеквизитов) Цикл
		СтруктураРеквизитов.Свойство(ИмяРеквизита, ЭтаФорма[ИмяРеквизита]);
	КонецЦикла;
	
	Если Параметры.ИмяСправочника = "Контрагенты" Тогда
	
		Если НЕ ОбработкаОбъект().ЭДО_Использовать1СЭДО() Тогда
			ОбновитьДеревоПодразделенийНаСервере();
			ОбновитьДополнительныеНастройкиНаСервере();
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьДеревоПодразделенийНаСервере(ДополнительныеПараметры=Неопределено)
	
    Если ОбработкаОбъект().ЭДО_Использовать1СЭДО() Тогда
		
		Для Индекс = 0 По ДополнительныеПараметры.МассивСсылок.ВГраница() Цикл
			
			Ссылка = ДополнительныеПараметры.МассивСсылок[Индекс];
			МассивСтруктурПодразделений = ДополнительныеПараметры.МассивДанных[Индекс];
			
			Если Ссылка = Параметры.Ссылка Тогда
								
				Если МассивСтруктурПодразделений.Количество() > 0 Тогда
					Дерево = ОбработкаОбъект().ЭДО_Преобразование_МассивСтруктурВДеревоЗначений(МассивСтруктурПодразделений,"ID_РодительПодразделения","");
				Иначе		
					Дерево = Новый ДеревоЗначений;
					Дерево.Колонки.Добавить("Наименование");
					Дерево.Колонки.Добавить("ID");
				КонецЕсли;
			Иначе	
				Возврат;
			КонецЕсли;
			
		КонецЦикла;
		
	Иначе
		Дерево = ОбработкаОбъект().ЭДО_СправочникМенеджер_ПолучитьДеревоЭлементов("ПодразделенияКонтрагентов",Параметры.Ссылка);
	КонецЕсли;
	
	мДеревоПодразделений = РеквизитФормыВЗначение("ДеревоПодразделений");
	мДеревоПодразделений.Строки.Очистить();
	ОбработкаОбъект().ЗаполнитьСтрокиДерева(Дерево,мДеревоПодразделений);
	ЗначениеВРеквизитФормы(мДеревоПодразделений,"ДеревоПодразделений");
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьДополнительныеНастройкиНаСервере()
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	Диадок_НастройкиКонтрагентов.Организация,
	|	Диадок_НастройкиКонтрагентов.ИмяСвойства,
	|	Диадок_НастройкиКонтрагентов.Значение
	|ИЗ
	|	РегистрСведений.Диадок_НастройкиКонтрагентов КАК Диадок_НастройкиКонтрагентов
	|ГДЕ
	|	Диадок_НастройкиКонтрагентов.Контрагент = &Контрагент"
	);
	
	Запрос.УстановитьПараметр("Контрагент",СсылкаНаЭлемент);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		
		НоваяСтрока = ТаблицаНастроекПоОрганизациям.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока,Выборка);
		
	КонецЦикла;
		
КонецПроцедуры

&НаСервере
Процедура ЗаписатьЭлементСправочника()
	
	СтруктураРеквизитов = Новый Структура;
	
	СтруктураРеквизитов.Вставить("СвязанныйСправочник1",		СвязанныйСправочник1);
	СтруктураРеквизитов.Вставить("СвязанныйСправочник2",		СвязанныйСправочник2);
	СтруктураРеквизитов.Вставить("СвязанныйСправочник3",		СвязанныйСправочник3);
	
	СтруктураРеквизитов.Вставить("ID_ОсновноеПодразделение",	ID_ОсновноеПодразделение);
	
	ОбработкаОбъект().ЭДО_СправочникМенеджер_СохранитьЭлемент("Контрагенты", Параметры.Ссылка, СтруктураРеквизитов);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаписатьИЗакрыть(Команда)
	
	ЗаписатьЭлементСправочника();	
	
	Если Параметры.ИмяСправочника = "ПодразделенияКонтрагентов" Тогда
		Оповестить("Диадок_Сохранение_Подразделение");
	ИначеЕсли Параметры.ИмяСправочника = "Контрагенты" Тогда
		Оповестить("Диадок_Сохранение_Контрагент");
	КонецЕсли;
	
	ЭтаФорма.Закрыть();
	
КонецПроцедуры

&НаКлиенте
Процедура ДеревоПодразделенийВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	Если НЕ ОсновнаяФорма().Параметры.Использовать1СЭДО Тогда
		
		СсылкаНаЭлемент = Элементы.ДеревоПодразделений.ТекущиеДанные.Ссылка;
		
		ПараметрыФормы=	Новый Структура;
		ПараметрыФормы.Вставить("Ссылка",							СсылкаНаЭлемент);
		ПараметрыФормы.Вставить("Новая",							ЗначениеЗаполнено(СсылкаНаЭлемент));
		ПараметрыФормы.Вставить("ИмяСправочника",					"ПодразделенияКонтрагентов");
		ПараметрыФормы.Вставить("ЗакрыватьПриЗакрытииВладельца",	Истина);
		
		ОткрытьФорму(ОсновнаяФорма().Модуль_Платформа().ПутьКФормам+"Контрагенты_ФормаЭлементаУправляемая", ПараметрыФормы, ЭтаФорма);
		
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "Диадок_Сохранение_Подразделение" 
		И Параметры.ИмяСправочника = "Контрагенты" Тогда
				
		ОбновитьДеревоПодразделенийНаСервере(Параметр);
		
	ИначеЕсли ИмяСобытия = "Диадок_Сохранение_Контрагент" Тогда	
		
		ЗаполнитьРеквизитыФормыНаСервере();
		ОбновитьДеревоПодразделенийНаСервере(Параметр);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьПодразделениеПоУмолчанию(Команда)
	
	ТекСтрока = Элементы.ДеревоПодразделений.ТекущиеДанные;
	
	Если НЕ ТекСтрока = Неопределено Тогда
		ID_ОсновноеПодразделение = ТекСтрока.ID;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура УдалитьПодразделениеПоУмолчанию(Команда)
	
	ID_ОсновноеПодразделение = "";
	
КонецПроцедуры

&НаКлиенте
Процедура СвязиПодразделенияПриСменеСтраницы(Элемент, ТекущаяСтраница)
	
	Если ОсновнаяФорма().Параметры.Использовать1СЭДО И ТекущаяСтраница.Имя = "ГруппаПодразделения" Тогда
		
		МассивСсылок = Новый Массив;
		МассивСсылок.Добавить(Параметры.Ссылка);
		
		ДополнительныеПараметры = Новый Структура("ИмяСобытия, ИмяСправочника, МассивСсылок", "Диадок_Сохранение_Подразделение", "Организации", МассивСсылок);
		ОписаниеОповещения = ОсновнаяФорма().НовыйОписаниеОповещения("ПолучитьМассивСтруктурПодразделений_1СЭДО", ОсновнаяФорма().Модуль_РаботаСКомпонентой(), ДополнительныеПараметры);
		ОсновнаяФорма().ВыполнитьДействиеПослеАвторизации(ОписаниеОповещения);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьССервера(Команда)
	
	ОписаниеОповещения = ОсновнаяФорма().НовыйОписаниеОповещения("СинхронизироватьЮрФизЛицо", ОсновнаяФорма().Модуль_РаботаСКомпонентой(), Параметры.Ссылка);
	ОсновнаяФорма().ВыполнитьДействиеПослеАвторизации(ОписаниеОповещения);
	Оповестить("Диадок_Сохранение_Контрагент");
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьССервераНаСервере()
	
	ЗаполнитьРеквизитыФормыНаСервере();
	ОбновитьДеревоПодразделенийНаСервере();
	
КонецПроцедуры
