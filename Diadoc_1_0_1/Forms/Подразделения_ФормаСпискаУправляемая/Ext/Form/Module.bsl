&НаСервере
Перем мОбработкаОбъект;
&НаКлиенте
Перем ОсновнаяФорма;

//{		Сервисные методы
	

// Заглушка для совместимости с управляемыми формами	
&НаСервере
Функция ОбработкаОбъект()
	
	Если мОбработкаОбъект=Неопределено Тогда
		мОбработкаОбъект = РеквизитФормыВЗначение("Объект");
	КонецЕсли;
	
	Возврат мОбработкаОбъект;
	
КонецФункции
	
&НаКлиенте
Функция ОсновнаяФорма() Экспорт

	Если ОсновнаяФорма=Неопределено Тогда
		ОсновнаяФорма = ВладелецФормы.ОсновнаяФорма();
	КонецЕсли;	
	
	Возврат ОсновнаяФорма;
	
КонецФункции

&НаСервере
Функция ЭтоУправляемаяФорма()
	
	Возврат (СтрДлина(ТипЗнч(ЭтаФорма))>5);
	
КонецФункции

//}		Сервисные методы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	ОбновитьСписокНаСервере();
	
	ЭтаФорма.Заголовок = "Подразделения: " + Параметры.ЮрФизЛицо;
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьСписокНаСервере()
	
	Если ОбработкаОбъект().ЭДО_Использовать1СЭДО() Тогда
		Возврат;
	КонецЕсли;
	
	ЮрФизЛицо = Параметры.ЮрФизЛицо;
	//Дерево = ОбработкаОбъект().ЭДО_СправочникМенеджер_ПолучитьДеревоЭлементов("ПодразделенияКонтрагентов",Параметры.ЮрФизЛицо);
	Если ЮрФизЛицо.ИмяСправочника = "Организации" Тогда
		Дерево = ОбработкаОбъект().ЭДО_СправочникМенеджер_ПолучитьДеревоЭлементов("ПодразделенияОрганизаций",	ЮрФизЛицо);
	ИначеЕсли ЮрФизЛицо.ИмяСправочника = "Контрагенты" Тогда
		Дерево = ОбработкаОбъект().ЭДО_СправочникМенеджер_ПолучитьДеревоЭлементов("ПодразделенияКонтрагентов",	ЮрФизЛицо);
	Иначе
		Возврат;	
	КонецЕсли;
	
	мДеревоПодразделений = РеквизитФормыВЗначение("ДеревоПодразделений");
	мДеревоПодразделений.Строки.Очистить();
	ОбработкаОбъект().ЗаполнитьСтрокиДерева(Дерево,мДеревоПодразделений);
	ЗначениеВРеквизитФормы(мДеревоПодразделений,"ДеревоПодразделений");
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьСписок(Команда)	
	ОбновитьСписокНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия="Диадок_Сохранение_Подразделение" Тогда
		ОбновитьСписок("");
	ИначеЕсли ИмяСобытия = "Диадок_СписокПодразделений_ЗагрузкаПодразделений" Тогда
		СформироватьДеревоПодразделенийНаСервере_1СЭДО(Параметр);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ДеревоПодразделенийВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	Если НЕ Элементы.ДеревоПодразделений.ТекущиеДанные=Неопределено Тогда
		ЭтаФорма.Закрыть(Элементы.ДеревоПодразделений.ТекущиеДанные);
	КонецЕсли;
	
	СтандартнаяОбработка = Ложь;
	
КонецПроцедуры


&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Если ОсновнаяФорма().Параметры.Использовать1СЭДО Тогда
		ПодключитьОбработчикОжидания("ОбработчикПослеОткрытияФормы", 0.1, Истина);		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработчикПослеОткрытияФормы()
	
	ПолучитьДанныеПоПодразделениям_1СЭДО();
	
КонецПроцедуры

&НаКлиенте
Процедура ПолучитьДанныеПоПодразделениям_1СЭДО()
	
	МассивСсылок = Новый Массив;
	МассивСсылок.Добавить(Параметры.Ссылка);
	
	ДополнительныеПараметры = Новый Структура("ИмяСобытия, МассивСсылок", "Диадок_СписокПодразделений_ЗагрузкаПодразделений", МассивСсылок);
	ОписаниеОповещения = ОсновнаяФорма().НовыйОписаниеОповещения("ПолучитьМассивСтруктурПодразделений_1СЭДО", ОсновнаяФорма().Модуль_РаботаСКомпонентой(), ДополнительныеПараметры);
	ОсновнаяФорма().ВыполнитьДействиеПослеАвторизации(ОписаниеОповещения);
		
КонецПроцедуры

&НаСервере
Процедура СформироватьДеревоПодразделенийНаСервере_1СЭДО(ПараметрОбработкиОповещения)
	
	Для Индекс = 0 По ПараметрОбработкиОповещения.МассивСсылок.ВГраница() Цикл
		
		Ссылка = ПараметрОбработкиОповещения.МассивСсылок[Индекс];
		МассивСтруктурПодразделений = ПараметрОбработкиОповещения.МассивДанных[Индекс];
		
		Если МассивСтруктурПодразделений.Количество() > 0 Тогда
			Дерево = ОбработкаОбъект().ЭДО_Преобразование_МассивСтруктурВДеревоЗначений(МассивСтруктурПодразделений,"ID_РодительПодразделения","");
		Иначе		
			Дерево = Новый ДеревоЗначений;
			Дерево.Колонки.Добавить("Наименование");
			Дерево.Колонки.Добавить("ID");
		КонецЕсли;
		
		мДеревоПодразделений = РеквизитФормыВЗначение("ДеревоПодразделений");
		мДеревоПодразделений.Строки.Очистить();
		ОбработкаОбъект().ЗаполнитьСтрокиДерева(Дерево,мДеревоПодразделений);
		ЗначениеВРеквизитФормы(мДеревоПодразделений,"ДеревоПодразделений");
		
	КонецЦикла;
	
КонецПроцедуры
