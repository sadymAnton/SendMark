&НаСервере
Перем ОбработкаОбъект;

&НаКлиенте
Перем ОсновнаяФорма;

&НаСервере
//инициализация модуля и его экспортных функций
Функция ОбработкаОбъект()

	Если ОбработкаОбъект=Неопределено Тогда
		ОбработкаОбъект = РеквизитФормыВЗначение("Объект");
	КонецЕсли;	
	
	Возврат ОбработкаОбъект;
	
КонецФункции

&НаКлиенте
Функция ОсновнаяФорма()

	Если ОсновнаяФорма=Неопределено Тогда
		ОсновнаяФорма = ВладелецФормы;
	КонецЕсли;	
	
	Возврат ОсновнаяФорма;
	
КонецФункции

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ПоказыватьПравуюЧастьФормы = Ложь;
	//ШиринаПравойЧастиФормы = ЭлементыФормы.ПанельОбновлениеДанныхМодуля.Ширина;
	
	ТекущаяВерсияМодуля = ОбработкаОбъект().ЭДО_Модуль_ОбщиеНастройки().СохраненнаяВерсияМодуля;
	НоваяВерсияМодуля = ОбработкаОбъект().ЭДО_НомерРелиза();
	ОбработкаОбъект().ЭДО_Модуль_ВывестиОписаниеОбновленийВТабличныйДокумент(ПолеТД_НовоеВВерсии);
	
	Если ЗначениеЗаполнено(ТекущаяВерсияМодуля) Тогда
		ЭтоЗапускСтаройВерсии = (ОбработкаОбъект().ЭДО_Служебные_СравнитьВерсии(ТекущаяВерсияМодуля, НоваяВерсияМодуля) = "ПерваяБольше");
	Иначе
		ЭтоЗапускСтаройВерсии = Ложь;
	КонецЕсли;
	
	Если ЭтоЗапускСтаройВерсии Тогда
		
		Элементы.ГруппаИнформацияОРелизе.ТекущаяСтраница = Элементы.СтраницаЗапускСтаройВерсии;
		Элементы.ФормаПродолжитьРаботу.Видимость = Ложь;
		
		Элементы.ПанельОбновлениеДанныхМодуля.Видимость = Ложь;
		Элементы.ПанельОбновлениеПодключаемогоМодуля.Видимость = Ложь;
			
	Иначе
			
		Элементы.ГруппаИнформацияОРелизе.ТекущаяСтраница = Элементы.СтраницаИнфоОРелизе;
		
		Если НЕ ОбработкаОбъект().ЭДО_Модуль_НеобходимоОбновлениеСохраненныхДанных() Тогда
			Элементы.ПанельОбновлениеДанныхМодуля.Видимость = Ложь;
		Иначе
			ПоказыватьПравуюЧастьФормы = Истина;
		КонецЕсли;
		
		СовместимостьПМ = ОбработкаОбъект().ЭДО_Модуль_ПроверитьСовместимостьВерсииAPIПодключаемогоМодуля();
		
		Если СовместимостьПМ.ВерсииСовместимы Тогда
			
			Элементы.ПанельОбновлениеПодключаемогоМодуля.Видимость = Ложь;
			
		Иначе
			
			ВерсияAPIОсновногоМодуля		= СовместимостьПМ.ВерсияAPIОсновногоМодуля;
			ВерсияAPIПодключаемогоМодуля	= СовместимостьПМ.ВерсияAPIПодключаемогоМодуля;
			ПоказыватьПравуюЧастьФормы = Истина;
			
		КонецЕсли;
		
	КонецЕсли;
	
	// Если ни одно обновление не требуется, то растянем панель с описанием обновлений
	Если Не ПоказыватьПравуюЧастьФормы Тогда
		
		Элементы.ГруппаПраваяПанель.Видимость = Ложь;
		//ЭлементыФормы.ПанельОписаниеОбновлений.Ширина = 
		//		ЭлементыФормы.ПанельОписаниеОбновлений.Ширина
		//		+ ШиринаПравойЧастиФормы;
				
	Иначе
		
		Элементы.ФормаПродолжитьРаботу.Видимость = Ложь;
		//ЭлементыФормы.ОсновныеДействияФормы.Кнопки.Удалить(
		//	ЭлементыФормы.ОсновныеДействияФормы.Кнопки.ПродолжитьРаботу);
			
		//ЭтаФорма.Ширина = ЭтаФорма.Ширина + ШиринаПравойЧастиФормы;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии()
	
	ВладелецФормы.Закрыть();
	
КонецПроцедуры

&НаКлиенте
Процедура мПоказатьПредупреждение(Парам1, Текст=Неопределено, Таймаут=0, Заголовок="")
	
	ОсновнаяФорма().Модуль_Платформа().ПоказатьПредупреждениеПереопределенная(Парам1, Текст, Таймаут, Заголовок);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработчикЗакрытияФормы(Параметры) Экспорт
	
	ЭтаФорма.Закрыть();
	
КонецПроцедуры

&НаКлиенте
Процедура ВыполнитьОбновлениеДанныхМодуля(Команда)
	
	РезультатОбновления = ВыполнитьОбновлениеДанныхМодуляНаСервере();
	
	Если Не ЗначениеЗаполнено(РезультатОбновления) Тогда
		// все ок, покажем пользователю котика и предложим ребутнуть модуль
		мПоказатьПредупреждение( , "Обновление данных выполнено успешно.
			|Перезапустите модуль.");
		Закрыть();
	Иначе
		// кажется, что-то пошло не так. Покажем, что именно.
		мПоказатьПредупреждение( , "Обновление данных не выполнено по причине:
			|" + РезультатОбновления);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ВыполнитьОбновлениеДанныхМодуляНаСервере()
	
	ОбработкаОбъект = РеквизитФормыВЗначение("Объект");
	Возврат ОбработкаОбъект.ЭДО_Модуль_ВыполнитьОбновлениеДанныхМодуля();
	
КонецФункции

&НаКлиенте
Процедура ПродолжитьРаботу(Команда)

	ПродолжитьРаботуНаСервере();
	Закрыть();
	//ПолучитьФорму("Форма").Открыть();
	// как там с переоткрытием основной формы?
	
КонецПроцедуры

&НаСервере
Процедура ПродолжитьРаботуНаСервере()
	
	ОбработкаОбъект().ЭДО_Модуль_УстановитьНовуюВерсиюМодуля();
	ОбработкаОбъект().ЭДО_Модуль_Финализировать();
	
КонецПроцедуры

&НаКлиенте
Процедура ДекорацияГиперссылкаИнструкцияПМНажатие(Элемент)
	
	ЗапуститьПриложение("https://wiki.diadoc.ru/pages/viewpage.action?pageId=7669921");
	
КонецПроцедуры

