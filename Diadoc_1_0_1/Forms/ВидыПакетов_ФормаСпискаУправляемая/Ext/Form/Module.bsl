&НаСервере
Перем мОбработкаОбъект;
&НаКлиенте
Перем ОсновнаяФорма;

//{		Сервисные методы
	

// Заглушка для совместимости с управляемыми формами	
&НаСервере
Функция ОбработкаОбъект()
	
	Если мОбработкаОбъект=Неопределено Тогда
		
		Если Не ЭтоУправляемаяФорма() Тогда
			мОбработкаОбъект = Вычислить("ОбработкаОбъект");
		Иначе
			мОбработкаОбъект = Вычислить("РеквизитФормыВЗначение(""Объект"")");
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат мОбработкаОбъект;
	
КонецФункции
	
&НаКлиенте
Функция ОсновнаяФорма() Экспорт

	Если ОсновнаяФорма = Неопределено Тогда
		ОсновнаяФорма= ВладелецФормы.ОсновнаяФорма();
	КонецЕсли;	
	
	Возврат ОсновнаяФорма;
	
КонецФункции

&НаСервере
Функция ЭтоУправляемаяФорма()
	
	Возврат (СтрДлина(ТипЗнч(ЭтаФорма))>5);
	
КонецФункции

//}		Сервисные методы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	ОбновитьСписокНаСервере();
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьСписокНаСервере()
	
	Список = ОбработкаОбъект().ЭДО_СправочникМенеджер_ПолучитьСписокЭлементов("ВидыПакетов");
	
	СправочникСписок.Очистить();
	
	Для Каждого Стр Из Список Цикл
		НоваяСтрока = СправочникСписок.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока,Стр);
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьСписок(Команда)
	ОбновитьСписокНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьФормуЭлементаДляРедактированияИлиДобавления(ЭтоДобавление=Ложь)
	
	ТекСтрока = Элементы.СправочникСписок.ТекущиеДанные;
	
	Если (ТекСтрока=Неопределено) и Не ЭтоДобавление Тогда
		Возврат;
	КонецЕсли;	
	
	СсылкаНаЭлемент = Неопределено;
	
	Если НЕ ТекСтрока=Неопределено 
		и не ЭтоДобавление Тогда
		
		СсылкаНаЭлемент = ТекСтрока.Ссылка;
		
	КонецЕсли;
	
	ОсновнаяФорма().Модуль_Платформа().ОткрытьФормуЭлементаДляРедактированияИлиДобавления("ВидыПакетов_ФормаЭлементаУправляемая", СсылкаНаЭлемент);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия="Диадок_Сохранение_ПакетДокументов" Тогда
		ОбновитьСписокНаСервере();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СправочникСписокВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	Если Параметры.ФормаОткрытаКакВыбор Тогда
		Если НЕ Элементы.СправочникСписок.ТекущиеДанные=Неопределено Тогда
			ЭтаФорма.Закрыть(Элементы.СправочникСписок.ТекущиеДанные.Ссылка);
		КонецЕсли;
	Иначе
		ОткрытьФормуЭлементаДляРедактированияИлиДобавления(Ложь);
	КонецЕсли;
	
	СтандартнаяОбработка = Ложь;
	
КонецПроцедуры

