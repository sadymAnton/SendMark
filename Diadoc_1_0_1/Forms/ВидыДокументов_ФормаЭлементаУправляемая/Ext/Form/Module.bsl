&НаКлиенте
Перем ОсновнаяФорма;
&НаСервере
Перем ОбработкаОбъект;

&НаКлиенте
Функция ОсновнаяФорма() Экспорт

	Если ОсновнаяФорма = Неопределено Тогда
		ОсновнаяФорма= ВладелецФормы.ОсновнаяФорма();
	КонецЕсли;	
	
	Возврат ОсновнаяФорма;
	
КонецФункции

&НаСервере
Функция ОбработкаОбъект()
	Если ОбработкаОбъект=Неопределено Тогда
		ОбработкаОбъект = РеквизитФормыВЗначение("Объект");
	КонецЕсли;
	Возврат ОбработкаОбъект;
КонецФункции

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	СсылкаНаЭлемент = Параметры.Ссылка;
	
	Если ЗначениеЗаполнено(СсылкаНаЭлемент) Тогда
		ЗаполнитьРеквизитыФормыНаСервере();
	КонецЕсли;
	
	Для Каждого Стр Из ОбработкаОбъект().ЭДО_ПредопределенныеСписки_Получить("ТипыДокументов") Цикл
		Элементы.ТипДокумента.СписокВыбора.Добавить(Стр.Наименование, Стр.Наименование + " (" + СокрЛП(Стр.Описание) + ")");
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьРеквизитыФормыНаСервере()
	
	СтруктураРеквизитов = ОбработкаОбъект().ЭДО_СправочникМенеджер_ПолучитьЭлемент("ВидыДокументов",СсылкаНаЭлемент);
	
	ЗаполнитьЗначенияСвойств(ЭтаФорма,СтруктураРеквизитов);
	
КонецПроцедуры

&НаСервере
Процедура ЗаписатьЭлементСправочника()
	
	СтруктураРеквизитов = Новый Структура("Наименование, ТипДокумента, Комментарий");
	ЗаполнитьЗначенияСвойств(СтруктураРеквизитов, ЭтаФорма);
	ОбработкаОбъект().ЭДО_СправочникМенеджер_СохранитьЭлемент("ВидыДокументов", СсылкаНаЭлемент, СтруктураРеквизитов);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаписатьИЗакрыть(Команда)
	
	ЗаписатьЭлементСправочника();	
	
	Оповестить("Диадок_Сохранение_ВидыЭлектронныхДокументов");
	
	ЭтаФорма.Закрыть();
	
КонецПроцедуры


&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ОсновнаяФорма().ПоказатьКнопкуВыпадающегоСписка_83(Элементы.ТипДокумента);
	
КонецПроцедуры

